apply plugin: 'kotlin2js'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group group_id
version vuekt_js2vue_version

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.musyozoku:vuekt:$vuekt_version"
    compile "xyz.nulldev:aza-kotlin-css-js:$aza_kotlin_css_version"
}

compileKotlin2Js {
    kotlinOptions {
        outputFile = "$buildDir/js/${project.name}.js"
        moduleKind = "umd"
        sourceMap = true
    }
}

// Skip tasks
compileJava.enabled = false
processResources.enabled = false

// Task dependencies
def publishVuektTask = tasks.getByPath(':vuekt:publishToMavenLocal')

build.dependsOn(publishVuektTask)
build.mustRunAfter(publishVuektTask)

// for publishToMavenLocal task
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

// for uploadArchives task (distribute with GitHub)
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:$releaseDir")
            pom.project {
                inceptionYear '2017'
                packaging 'jar'
                licenses {
                    license {
                        name licence_name
                        url licence_url
                        distribution 'repo'
                    }
                }
            }
        }
    }
}