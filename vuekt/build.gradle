apply plugin: 'kotlin2js'
apply plugin: 'com.moowork.node'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group group_id
version vuekt_version

// Skip tasks
compileJava.enabled = false
processResources.enabled = false

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

node {
    download = true
}

task ts2kt(type: NodeTask) {
    String nodeDir = "${project.projectDir}/node_modules"
    script = file("$nodeDir/ts2kt/ts2kt.js")

    String destDir = 'src/main/kotlin/org/vuejs'
    file(destDir).mkdir()

    args = ['-d', destDir] + file("$nodeDir/vue/types").listFiles().collect { it.absolutePath }
}

compileKotlin2Js {
    kotlinOptions {
        outputFile = "$buildDir/js/${project.name}.js"
        moduleKind = "umd"
        sourceMap = true
    }
}

// for publishToMavenLocal task
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

// for uploadArchives task (distribute with GitHub)
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:$releaseDir")
            pom.project {
                inceptionYear '2017'
                packaging 'jar'
                licenses {
                    license {
                        name licence_name
                        url licence_url
                        distribution 'repo'
                    }
                }
            }
        }
    }
}