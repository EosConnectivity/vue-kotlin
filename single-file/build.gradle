apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'org.musyozoku.vuekt'

ext {
    enableSourceMap = false
}

// Skip tasks
classes.enabled = false
jar.enabled = false

dependencies {
    compile project(':single-file:greeting')
    compile project(':single-file:greeting-component')
}

compileKotlin2Js {
    kotlinOptions {
        sourceMap = enableSourceMap
    }
}

compileTestKotlin2Js {
    kotlinOptions {
        sourceMap = enableSourceMap
    }
}

kotlinFrontend {
    downloadNodeJsVersion = node_version
    sourceMaps = enableSourceMap

    npm {
        dependency('vue', vue_version)

        devDependency('vue-loader', '13.0.2')
        devDependency('vue-template-compiler', vue_version)
        devDependency('css-loader', '*')
        devDependency('js-beautify', '1.6.14') // TODO remove
        devDependency('html-webpack-plugin', '2.30.1')
    }

    webpackBundle {
        contentPath = file("$projectDir/webContent")
        publicPath = "/"
        port = 8088
        stats = "errors-only"
    }
}

subprojects {

    apply plugin: 'kotlin2js'

    // Skip tasks
    classes.enabled = false
    jar.enabled = false

    // Kotlin/JS library dependencies
    dependencies {
        compile "org.musyozoku:vuekt:$vuekt_version"
        compile "org.musyozoku:vuekt-js2vue:$vuekt_js2vue_version"
        compile "xyz.nulldev:aza-kotlin-css-js:$aza_kotlin_css_version"
    }

    sourceSets {
        main.kotlin.srcDirs += "main"
        test.kotlin.srcDirs += "test"
    }

    compileKotlin2Js {
        kotlinOptions {
            moduleKind = "commonjs"
            sourceMap = enableSourceMap
        }
    }
}

// Task dependencies
def publishVuektTask = tasks.getByPath(':vuekt:publishToMavenLocal')
def publishVuektJs2VueTask = tasks.getByPath(':vuekt-js2vue:publishToMavenLocal')

packages.dependsOn(publishVuektTask)
packages.dependsOn(publishVuektJs2VueTask)
packages.mustRunAfter(publishVuektTask)
packages.mustRunAfter(publishVuektJs2VueTask)