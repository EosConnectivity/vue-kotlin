apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'

ext {
    enableSourceMap = false
}

// Skip tasks
classes.enabled = false
jar.enabled = false

dependencies {
    compile project(':single-file:greeting')
    compile project(':single-file:greeting-component')
}

compileKotlin2Js {
    kotlinOptions {
        sourceMap = enableSourceMap
    }
}

compileTestKotlin2Js {
    kotlinOptions {
        sourceMap = enableSourceMap
    }
}

kotlinFrontend {
    downloadNodeJsVersion = node_version
    sourceMaps = enableSourceMap

    npm {
        dependency('vue', vue_version)

        devDependency('vue-loader', '13.0.2')
        devDependency('vue-template-compiler', vue_version)
        devDependency('css-loader', '*')
        devDependency('js-beautify', '1.6.14') // TODO remove
        devDependency('html-webpack-plugin', '2.30.1')
    }

    webpackBundle {
        contentPath = file("$projectDir/webContent")
        publicPath = "/"
        port = 8088
        stats = "errors-only"
    }
}

task js2vue(type: Exec) { // TODO to plugin

    executable "node"
    args("greeting-component_main.js")
    workingDir "$projectDir/greeting-component/build/classes/main"
    environment("NODE_PATH", "$buildDir/node_modules")
    environment("PATH", "~/.gradle/nodejs/node-v8.5.0-darwin-x64/bin") // TODO use kotlin-frontend-plugin setting
}

subprojects {

    apply plugin: 'kotlin2js'

    // Skip tasks
    classes.enabled = false
    jar.enabled = false

    // Kotlin/JS library dependencies
    dependencies {
        compile "org.musyozoku:vuekt:1.0-SNAPSHOT"
        compile 'xyz.nulldev:aza-kotlin-css-js:1.0'
    }

    sourceSets {
        main.kotlin.srcDirs += "main"
        test.kotlin.srcDirs += "test"
    }

    compileKotlin2Js {
        kotlinOptions {
            moduleKind = "commonjs"
            sourceMap = enableSourceMap
        }
    }
}

// Task dependencies
def publishVuektTask = tasks.getByPath(':vuekt:publishToMavenLocal')

packages.dependsOn(publishVuektTask)
tasks.js2vue.dependsOn(tasks.compileKotlin2Js)
tasks.bundle.dependsOn(tasks.js2vue)
tasks.run.dependsOn(tasks.js2vue)