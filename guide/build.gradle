ext {
    outputDir = "$buildDir/js/"
}

subprojects {

    repositories {
        flatDir {
            dirs vuektLibDir
        }
    }

    dependencies {
        compile "org.musyozoku:vuekt:1.0-SNAPSHOT"
    }

    sourceSets {
        main.kotlin.srcDirs += "main"
        test.kotlin.srcDirs += "test"
    }

    compileKotlin2Js {
        afterEvaluate { project ->
            kotlinOptions {
                outputFile = "$buildDir/${project.name}.js"
                moduleKind = "umd"
                sourceMap = false
            }
        }
    }

    build.doLast {
        copy {
            from buildDir
            into outputDir
            include '*.js'
            include '*.map'
        }

        delete buildDir
    }
}

repositories {
    flatDir {
        dirs vuektLibDir
    }
}

dependencies {
    compile 'org.webjars:requirejs:2.3.3'
    compile 'org.webjars:vue:2.1.3'
    compile 'org.musyozoku:vuekt:1.0-SNAPSHOT'
}

build.doLast() {
    // Copy *.js from jar into build directory
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into outputDir
            include '**/*.js'
            eachFile { details ->
                def names = details.path.split('/')
                details.path = names.getAt(names.length - 1)
            }
        }
    }
}